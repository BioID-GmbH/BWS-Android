apply plugin: 'com.android.application'

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId 'com.bioid.authenticator'
        minSdkVersion 23
        targetSdkVersion 27
        versionCode commitCountOfCurrentBranch()
        versionName nameOfLastTag()

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // additional information about the build
        buildConfigField 'String', 'COMMIT_SHA', "\"${commitSha()}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared"
        }
        androidTest {
            java.srcDirs += "$projectDir/src/testShared"
        }
    }

    flavorDimensions 'identity_provider'

    productFlavors {
        bws {
            dimension 'identity_provider'
            applicationIdSuffix '.bws'  // making it possible to have both the connect and bws flavor installed in parallel

            // adding BWS settings from local gradle.properties
            buildConfigField 'String', 'BIOID_BWS_INSTANCE_NAME', BioIdBwsInstanceName
            buildConfigField 'String', 'BIOID_APP_ID', BioIdAppId
            buildConfigField 'String', 'BIOID_APP_SECRET', BioIdAppSecret
            buildConfigField 'String', 'BIOID_BCID', BioIdBcid
        }
    }
}

dependencies {
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.google.android.gms:play-services-vision:15.0.2'
    implementation 'org.rajawali3d:rajawali:1.1.970@aar'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.18.3'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:2.4.6'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'org.mockito:mockito-android:2.18.3'
}

/**
 * Does count the amount of commits on the current branch.
 *
 * This should be unique enough for the android version code.
 * The only downside is that if hotfix releases on another branch are necessary the next release from master has to have more
 * commits in total (which should be the case normally).
 */
static def commitCountOfCurrentBranch() {
    return Integer.parseInt(['git', 'rev-list', '--count', 'HEAD'].execute().text.trim())
}

/**
 * Returns the name of the last tag plus a "-dirty" suffix if any commits or working directory changes have been made.
 */
static def nameOfLastTag() {
    def gitDescribe = ['git', 'describe', '--dirty', '--long'].execute().text.trim()  // e.g. "1.2.1-3-gb076164-dirty"

    if (gitDescribe.isEmpty()) {
        // no tagged revision in this repository
        return 'dirty'
    }

    def split = gitDescribe.split('-')
    def lastTag = split[0]
    def commitCount = Integer.parseInt(split[1])
    def dirtyFlagPresent = split.length == 4

    if (commitCount > 0 || dirtyFlagPresent) {
        return lastTag + '-dirty'
    } else {
        return lastTag
    }
}

/**
 * Returns the SHA-1 commit hash of the HEAD.
 */
static def commitSha() {
    return ['git', 'rev-parse', '--short', 'HEAD'].execute().text.trim()
}
